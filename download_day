#!/usr/bin/env fish

argparse 'd/day=' 'y/year=' -- $argv
set -f DAY_NUM "$_flag_day"
set -f YEAR "$_flag_year"

if test -z "$DAY_NUM"
    set -f DAY_NUM "$(date +"%-d")"
end
if test -z "$YEAR"
    set -f YEAR "$(date +"%Y")"
end

set -f YEAR_DIR "$YEAR/data"
if not test -d $YEAR_DIR
    # If directory does not exist - create it
    echo "Creating directory '$YEAR_DIR'..."
    mkdir $YEAR_DIR -p
end

if not test -f "session_cookie.txt"
    read -l --prompt-str "Please provide session cookie: " cookie
    echo "session=$cookie" > session_cookie.txt
end
set -l SESSION_COOKIE (cat session_cookie.txt)
set -l data_file "$YEAR_DIR/day$DAY_NUM.txt"
echo -e "Downloading data into '$data_file'..."
curl -H "Cookie: $SESSION_COOKIE" https://adventofcode.com/$YEAR/day/$DAY_NUM/input --output $data_file

echo -e "\nPreview:" && head $data_file && echo "..."
